plugins {
  id "com.android.application"
  id "org.jetbrains.kotlin.android"
  id "org.jetbrains.kotlin.plugin.compose"
}

android {
  namespace "to.holepunch.bare.android"
  compileSdk 34
  ndkVersion "27.2.12479018"

  defaultConfig {
    applicationId "to.holepunch.bare.android"
    minSdk 28
    targetSdk 35
    versionCode 1
    versionName "1.0"
  }

  buildFeatures {
    compose true
  }

  compileOptions {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
  }

  kotlinOptions {
    jvmTarget = "17"
  }

  sourceSets {
    main {
      jniLibs.srcDirs "src/main/addons", "libs/bare-kit/jni"
    }
  }
}

tasks.register("link", Exec) {
  workingDir ".."

  commandLine([
    "npx", "bare-link",
    "--target", "android-arm",
    "--target", "android-arm64",
    "--target", "android-ia32",
    "--target", "android-x64",
    "--needs", "libbare-kit.so",
    "--out", "app/src/main/addons"
  ])
}

tasks.register("pack", Exec) {
  workingDir ".."

  commandLine([
    "npx", "bare-pack",
    "--target", "android-arm",
    "--target", "android-arm64",
    "--target", "android-ia32",
    "--target", "android-x64",
    "--linked",
    "--out", "app/src/main/assets/app.bundle",
    "app/src/main/js/app.js"
  ])
}

preBuild.dependsOn link, pack

dependencies {
  api fileTree(dir: "libs", include: ["bare-kit/classes.jar"])
  implementation('androidx.compose.material3:material3:1.3.1')
  implementation('androidx.compose.ui:ui:1.7.6')
  implementation('androidx.compose.ui:ui-tooling-preview:1.7.6')
  implementation('androidx.activity:activity-compose:1.9.3')

}
