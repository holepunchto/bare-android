plugins {
  id "com.android.application"
  id "org.jetbrains.kotlin.android"
  id "com.google.gms.google-services"
}

android {
  namespace = "to.holepunch.bare.android"
  compileSdk = 35
  ndkVersion = "27.2.12479018"

  defaultConfig {
    applicationId = "to.holepunch.bare.android"
    minSdk = 33
    targetSdk = 35
    versionCode = 1
    versionName = "1.0"
  }

  compileOptions {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
  }

  kotlinOptions {
    jvmTarget = "17"
  }

  sourceSets {
    main {
      jniLibs.srcDirs "src/main/addons", "libs/bare-kit/jni"
    }
  }
}

tasks.register("link", Exec) {
  workingDir ".."

  commandLine([
    "node_modules/.bin/bare-link",
    "--preset", "android",
    "--needs", "libbare-kit.so",
    "--out", "app/src/main/addons"
  ])
}

tasks.register("packApp", Exec) {
  workingDir ".."

  commandLine([
    "node_modules/.bin/bare-pack",
    "--preset", "android",
    "--out", "app/src/main/assets/app.bundle",
    "app/src/main/js/app.js"
  ])
}

tasks.register("packPush", Exec) {
  workingDir ".."

  commandLine([
    "node_modules/.bin/bare-pack",
    "--preset", "android",
    "--out", "app/src/main/assets/push.bundle",
    "app/src/main/js/push.js"
  ])
}

tasks.register("pack") {
  dependsOn packApp, packPush
}

preBuild.dependsOn link, pack

dependencies {
  api fileTree(dir: "libs", include: ["bare-kit/classes.jar"])
  implementation platform("com.google.firebase:firebase-bom:33.10.0")
  implementation "com.google.firebase:firebase-messaging"
}
